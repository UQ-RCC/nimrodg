buildscript {
	repositories {
		jcenter()
	}

	dependencies {
		classpath 'com.netflix.nebula:gradle-ospackage-plugin:6.1.2'
		classpath 'de.undercouch:gradle-download-task:3.4.3'
	}
}

apply plugin: 'distribution'
apply plugin: 'application'
apply plugin: 'nebula.rpm'
apply plugin: 'nebula.deb'
apply plugin: 'de.undercouch.download'

mainClassName = 'au.edu.uq.rcc.nimrodg.cli.NimrodCLI'

configurations {
	/* Packages that need to be loaded by Kepler. */
	nimrodK
	/* Packages that should be in a separate classloader. */
	nimrodKRuntime
	nimrodKRuntime.extendsFrom nimrodK
}

dependencies {
	implementation project(':nimrodg-cli')
	nimrodK project(':nimrodg-api')
	nimrodK group: 'org.glassfish', name: 'javax.json', version: JAVAX_JSON_VERSION
	nimrodKRuntime project(':nimrodg-parsing')
	nimrodKRuntime project(':nimrodg-impl-postgres')
}

distributions {
	main {
		baseName = 'nimrod'
		contents {
			from project(':nimrodg-cli').jar
			
			/* This is empty, exclude it */
			exclude jar.archiveName

			/* Fix the permissions on the jars. */
			filesMatching('*.jar') {
				it.mode(0644)
			}
		}
		distZip.enabled = false
	}
}

task buildNimrodKFatJar(type: Jar, dependsOn: configurations.nimrodKRuntime) {
	baseName = 'nimrod-kepler-fatjar'
	version = ''
	extension = 'nimjar'
	from {
		(configurations.nimrodKRuntime - configurations.nimrodK).collect {
			it.isDirectory() ? it : zipTree(it)
		}
	}

	exclude 'module-info.class'
	exclude 'META-INF/'
	exclude 'Log4j-charsets.properties'
}

task generateNimrodKModule(type: Copy, dependsOn: [buildNimrodKFatJar, configurations.nimrodK]) {
	configurations.nimrodK.each {
		from(it) into ("${buildDir}/nimrodk/lib/jar/nimrodg")
	}

	from(buildNimrodKFatJar.archivePath) into ("${buildDir}/nimrodk/lib/jar/nimrodg")
}

ext {
	/* TODO: Move all of this to a separate file builddef.gradle or something. */
	packageSummary = 'A specialised parametric modelling system.'
	packageDescription = '''It uses a simple declarative parametric modelling language to express
a parametric experiment.

It provides the machinery to automate the task of formulating, running,
monitoring, collating, presenting and visualising the results from
multiple individual experiments.

Nimrod incorporates distributed scheduling so that the appropriate
number and kind of resources to complete the job, e.g., HPC and
virtual machines, can be selected and used.

Nimrod helps researchers run computations remotely on the cloud. It can
turn your laptop into a supercomputer. With Nimrod you can run many
jobs â€” millions if need be.
'''
	packageMaintainer = 'Research Computing Centre <rcc-admin@uq.edu.au>'
	packageUrl = 'https://rcc.uq.edu.au/nimrod'

	packageInstallDir = '/usr/share/nimrod'

	agentVersion = '1.1.1'
	agentList = [
		[platform:'x86_64-pc-linux-musl',		sha256:'ede1a9b56342f8c91952e0ce6bc6a0fea5b0e1ab8cb954edf28203fa98743f1a'],
		[platform:'i686-pc-linux-musl',			sha256:'44d4bec004214b928ab81af16e6078e94553f15d9da2109dae3c2b125dd5a1e7'],
		[platform:'armv8-rpi3-linux-gnueabihf',	sha256:'d4c1073cb284e1ca0fbe869cedc0e83ca809f6cc34db941f16cfcc0be4007408'],
		[platform:'armv6-rpi-linux-gnueabi',	sha256:'558d07d33983943f28506383dfab3c4ccbc05fd913210e77bad3caaed63dc8f7']
	]
}

/* Create download and verify tasks for each agent. */
agentList.each { agent ->

	def destFile = "${buildDir}/agents/agent-${agent.platform}"

	tasks.create(name: "downloadAgent${agent.platform}", type: Download) {
		src "https://github.com/UQ-RCC/nimrodg-agent/releases/download/${project.agentVersion}/agent-${agent.platform}-${project.agentVersion}"
		dest destFile
		onlyIfModified true
	}

	tasks.create(name: "verifyAgent${agent.platform}", type: Verify, dependsOn: "downloadAgent${agent.platform}") {
		src destFile
		algorithm 'SHA-256'
		checksum agent.sha256
	}

	/* Add them to the distribution */
	distributions.main.contents.with {
		from(destFile) {
			into 'agents'
			fileMode 0755
		}
	}

	[distTar, distZip].each { it.dependsOn += "verifyAgent${agent.platform}" }
}


task buildDeb(type: Deb, dependsOn: assembleDist) {
	summary				project.packageSummary
	packageDescription	project.packageDescription

	version				"${project.version}-0ubuntu1"
	maintainer			project.packageMaintainer
	arch				'all'
	url					project.packageUrl

	requires			'openjdk-11-jre-headless'
	recommends			'postgresql-10'
	recommends			'rabbitmq-server'

	user				'root'
	permissionGroup		'root'


	into project.packageInstallDir

	def distContents = copySpec {
		with distributions.main.contents

		/* Lintian gets cranky about this. */
		exclude 'nimrod.bat'
	}
	with distContents

	from('setup-defaults.ini') {
		into '/etc/xdg/nimrod'
		fileMode 0644
	}

	configurationFile('/etc/xdg/nimrod/setup-defaults.ini')

	link('/usr/bin/nimrod', "${project.packageInstallDir}/bin/nimrod")
}

task buildRpm(type: Rpm, dependsOn: assembleDist) {
	summary				project.packageSummary
	packageDescription	project.packageDescription

	license				'ASL 2.0'
	release				'1'
	arch				NOARCH
	os					LINUX
	type				BINARY
	packager			project.packageMaintainer
	url					project.packageUrl
	vendor				'Research Computing Centre'

	requires			'java-11-openjdk-headless'

	user				'root'
	permissionGroup		'root'

	into project.packageInstallDir

	def distContents = copySpec {
		with distributions.main.contents

		/* Lintian gets cranky about this. */
		exclude 'nimrod.bat'
	}

	with distContents

	from('setup-defaults.ini') {
		into '/etc/xdg/nimrod'
		fileMode 0644
		fileType CONFIG
	}

	link('/usr/bin/nimrod', "${project.packageInstallDir}/bin/nimrod")
}

task(generateArtifacts, dependsOn:[assembleDist, buildDeb, buildRpm]) {
}

