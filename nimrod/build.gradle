buildscript {
	repositories {
		jcenter()
	}

	dependencies {
		classpath 'com.netflix.nebula:gradle-ospackage-plugin:6.1.2'
		classpath 'de.undercouch:gradle-download-task:3.4.3'
	}
}

apply plugin: 'distribution'
apply plugin: 'application'
apply plugin: 'nebula.rpm'
apply plugin: 'nebula.deb'
apply plugin: 'de.undercouch.download'

mainClassName = 'au.edu.uq.rcc.nimrodg.cli.NimrodCLI'

configurations {
	/* Packages that need to be loaded by Kepler. */
	nimrodK
	/* Packages that should be in a separate classloader. */
	nimrodKRuntime
	nimrodKRuntime.extendsFrom nimrodK
}

dependencies {
	implementation project(':nimrodg-cli')
	nimrodK project(':nimrodg-api')
	nimrodK group: 'org.glassfish', name: 'javax.json', version: JAVAX_JSON_VERSION
	nimrodKRuntime project(':nimrodg-parsing')
	nimrodKRuntime project(':nimrodg-impl-postgres')
}

distributions {
	main {
		baseName = 'nimrod'
		contents {
			from project(':nimrodg-cli').jar
			
			/* This is empty, exclude it */
			exclude jar.archiveName

			/* Fix the permissions on the jars. */
			filesMatching('*.jar') {
				it.mode(0644)
			}
		}
		distZip.enabled = false
	}
}

distTar {
	compression = Compression.GZIP
	extension = 'tar.gz' /* Not .tgz */
}

task buildNimrodKFatJar(type: Jar, dependsOn: configurations.nimrodKRuntime) {
	baseName = 'nimrod-kepler-fatjar'
	version = ''
	extension = 'nimjar'
	from {
		(configurations.nimrodKRuntime - configurations.nimrodK).collect {
			it.isDirectory() ? it : zipTree(it)
		}
	}

	exclude 'module-info.class'
	exclude 'META-INF/'
	exclude 'Log4j-charsets.properties'
}

task generateNimrodKModule(type: Copy, dependsOn: [buildNimrodKFatJar, configurations.nimrodK]) {
	configurations.nimrodK.each {
		from(it) into ("${buildDir}/nimrodk/lib/jar/nimrodg")
	}

	from(buildNimrodKFatJar.archivePath) into ("${buildDir}/nimrodk/lib/jar/nimrodg")
}

ext {
	/* TODO: Move all of this to a separate file builddef.gradle or something. */

	/* https://github.com/jgitver/jgitver/blob/master/src/main/java/fr/brouillard/oss/jgitver/metadata/Metadatas.java#L25 */
	isDevBuild = Integer.valueOf(project.commit_distance) > 0
	if(isDevBuild) {
		packageName = 'nimrod-dev'
	} else {
		packageName = 'nimrod'
	}

	/* The version number. */
	packageVersion = "${project.base_version}"
	/* Everything after the version number. */
	packageRelease = "${(project.version =~ /^\d+\.\d+\.\d+-(.+)$/)[0][1]}"

	packageSummary = 'A specialised parametric modelling system.'
	packageDescription = '''It uses a simple declarative parametric modelling language to express
a parametric experiment.

It provides the machinery to automate the task of formulating, running,
monitoring, collating, presenting and visualising the results from
multiple individual experiments.

Nimrod incorporates distributed scheduling so that the appropriate
number and kind of resources to complete the job, e.g., HPC and
virtual machines, can be selected and used.

Nimrod helps researchers run computations remotely on the cloud. It can
turn your laptop into a supercomputer. With Nimrod you can run many
jobs â€” millions if need be.
'''
	packageVendor = 'Research Computing Centre'
	packageMaintainer = "${packageVendor} <rcc-admin@uq.edu.au>"

	packageUrl = 'https://rcc.uq.edu.au/nimrod'

	packageInstallDir = '/usr/share/nimrod'
	packageConfigDir = '/etc/xdg/nimrod'

	agentVersion = '1.2.0'
	agentList = [
		[platform:'x86_64-pc-linux-musl',		sha256:'bd1c884a52b98abf11159b59916a736abf8fa5f112c149397de6973c10bad0e7'],
		[platform:'i686-pc-linux-musl',			sha256:'c9c914863c758c421d7ef95fa6bbb19684934d0cc3e16218a7143e76042139fd']
	]
}

/* Create download and verify tasks for each agent. */
agentList.each { agent ->

	def destFile = "${buildDir}/agents/agent-${agent.platform}"

	tasks.create(name: "downloadAgent${agent.platform}", type: Download) {
		src "https://github.com/UQ-RCC/nimrodg-agent/releases/download/${project.agentVersion}/agent-${agent.platform}-${project.agentVersion}"
		dest destFile
		onlyIfModified true
	}

	tasks.create(name: "verifyAgent${agent.platform}", type: Verify, dependsOn: "downloadAgent${agent.platform}") {
		src destFile
		algorithm 'SHA-256'
		checksum agent.sha256
	}

	/* Add them to the distribution */
	distributions.main.contents.with {
		from(destFile) {
			into 'agents'
			fileMode 0755
		}
	}

	[distTar, distZip].each { it.dependsOn += "verifyAgent${agent.platform}" }
}

def distContents = copySpec {
	with distributions.main.contents

	/* Lintian gets cranky about this. */
	exclude 'nimrod.bat'
}

def distConfigs = copySpec {
	def nimres = project(':nimrodg-resources')

	from('setup-defaults.ini')
	from('hpc.json')
	from(nimres.file("src/main/resources/au/edu/uq/rcc/nimrodg/resource/cluster/hpc.pbspro.j2"))
	from(nimres.file("src/main/resources/au/edu/uq/rcc/nimrodg/resource/cluster/hpc.slurm.j2"))
	from(nimres.file("src/main/resources/au/edu/uq/rcc/nimrodg/resource/cluster/hpc.lsf.j2"))

	fileMode 0644

	into packageConfigDir

//	File f = new File("${destinationDir}/hpc.json")
//	def hpc = new groovy.json.JsonSlurper().parseText(f.text)
//
//	/* Rewrite hpc.json to reference the external templates. */
//	hpc.each {
//		it.value.remove('template')
//		it.value.remove('template_file')
//		it.value.remove('template_classpath')
//		it.value.template_file = "${packageConfigDir}/hpc.${it.key}.j2"
//	}
//
//	f.text = groovy.json.JsonOutput.prettyPrint(groovy.json.JsonOutput.toJson(hpc))
//
//	from(f)
}

import org.redline_rpm.header.Flags

task buildDeb(type: Deb, dependsOn: assembleDist) {
	packageName			project.packageName

	summary				project.packageSummary
	packageDescription	project.packageDescription

	version				"${project.version}-0ubuntu1"
	maintainer			project.packageMaintainer
	arch				'all'
	url					project.packageUrl

	requires			'default-jre', '1.10', Flags.GREATER | Flags.EQUAL
	suggests			'postgresql', '10', Flags.GREATER | Flags.EQUAL
	suggests			'rabbitmq-server', '3.5.7', Flags.GREATER | Flags.EQUAL

	user				'root'
	permissionGroup		'root'


	into project.packageInstallDir

	with distContents

	with distConfigs
	distConfigs.eachFile { configurationFile("/${it.path}") }

	link('/usr/bin/nimrod', "${project.packageInstallDir}/bin/nimrod")
}

task buildRpmCentOS76(type: Rpm, dependsOn: assembleDist) {
	packageName			project.packageName

	summary				project.packageSummary
	packageDescription	project.packageDescription

	license				'ASL 2.0'
	version				project.packageVersion
	release				"${project.packageRelease.replace('-', '~')}.el7_6"
	arch				NOARCH
	os					LINUX
	type				BINARY
	packager			project.packageMaintainer
	url					project.packageUrl
	vendor				project.packageVendor

	requires			'java-11-openjdk-headless'

	user				'root'
	permissionGroup		'root'

	into project.packageInstallDir

	with distContents

	with distConfigs
	/* FIXME: This doesn't work. */
	distConfigs.eachFile {
		fileType CONFIG
	}

	link('/usr/bin/nimrod', "${project.packageInstallDir}/bin/nimrod")
}

task(generateArtifacts, dependsOn:[assembleDist, buildDeb, buildRpmCentOS76]) {
}

